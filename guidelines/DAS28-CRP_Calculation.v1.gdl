(GUIDE) <
	gdl_version = <"0.1">
	id = <"DAS28-CRP_Calculation.v1">
	concept = <"gt0001">
	language = (LANGUAGE) <
		original_language = <[ISO_639-1::en]>
	>
	description = (RESOURCE_DESCRIPTION) <
		details = <
			["en"] = (RESOURCE_DESCRIPTION_ITEM) <
				copyright = <"© Cambio Healthcare Systems">
				keywords = <"rheumatoid arthritis", "PaGDA", "tender joint count", "swollen joint count", "DAS28-CRP">
				purpose = <"This guide calculates the disesase activity score 28-CRP (DAS28-CRP) which is a measure of disease activity and treatment response in individuals with rheumatoid arthritis.">
				use = <"Use to calculate DAS28-CRP, based on four input parameters: tender joint count (TJC), swollen joint count (SJC), patient global assessment of disease activity (PtGDA), and level of C-reactive protein (in mg/dl).

DAS28-CRP = (0.56*√(TJC)+0.28*√(SJC)+0.36*ln(CRP + 1)+0.014*PtGDA +0.96)

DAS28-CRP is sometimes calculated using a PtGDA visual analogue scale of 0-100 (mm). This guide however uses a PtGDA visual analogue scale of 0-10 (cm). The necessary factoring has been incorporated in the application.">
			>
		>
		original_author = <
			["date"] = <"2016-11-07">
			["email"] = <"info@cambio.se">
			["name"] = <"Eneimi Allwell-Brown">
			["organisation"] = <"Cambio Healthcare Systems">
		>
		other_details = <
			["references"] = <"Anderson J, Caplan L, Yazdany J, Robbins ML, Neogi T, Michaud K, Saag KG, O'dell JR, Kazi S. Rheumatoid arthritis disease activity measures: American College of Rheumatology recommendations for use in clinical practice. Arthritis care & research. 2012 May 1;64(5):640-7.

Wells G, Becker JC, Teng J, Dougados M, Schiff M, Smolen J, Aletaha D, Van Riel PL. Validation of the 28-joint Disease Activity Score (DAS28) and European League Against Rheumatism response criteria based on C-reactive protein against disease progression in patients with rheumatoid arthritis, and comparison with the DAS28 based on erythrocyte sedimentation rate. Annals of the rheumatic diseases. 2009 Jun 1;68(6):954-60.">
		>
	>
	definition = (GUIDE_DEFINITION) <
		archetype_bindings = <
			["gt0003"] = (ARCHETYPE_BINDING) <
				archetype_id = <"openEHR-EHR-OBSERVATION.disease_activity_score_28_crp.v1">
				domain = <"CDS">
				elements = <
					["gt0004"] = (ELEMENT_BINDING) <
						path = <"/data[at0001]/events[at0002]/data[at0003]/items[at0004]">
					>
					["gt0010"] = (ELEMENT_BINDING) <
						path = <"/data/events/time">
					>
				>
			>
			["gt0005"] = (ARCHETYPE_BINDING) <
				archetype_id = <"openEHR-EHR-OBSERVATION.disease_activity_index_joint_score.v1">
				domain = <"EHR">
				elements = <
					["gt0006"] = (ELEMENT_BINDING) <
						path = <"/data[at0001]/events[at0002]/data[at0003]/items[at0043]">
					>
					["gt0007"] = (ELEMENT_BINDING) <
						path = <"/data[at0001]/events[at0002]/data[at0003]/items[at0044]">
					>
				>
				predicates = <"max(/data/events/time)",...>
			>
			["gt0008"] = (ARCHETYPE_BINDING) <
				archetype_id = <"openEHR-EHR-OBSERVATION.lab_test_c_reactive_protein.v1">
				domain = <"EHR">
				elements = <
					["gt0009"] = (ELEMENT_BINDING) <
						path = <"/data[at0001]/events[at0002]/data[at0003]/items[at0006]">
					>
				>
				predicates = <"max(/data/events/time)",...>
			>
			["gt0011"] = (ARCHETYPE_BINDING) <
				archetype_id = <"openEHR-EHR-OBSERVATION.patient_global_assessment_arthritis_activity.v1">
				domain = <"EHR">
				elements = <
					["gt0012"] = (ELEMENT_BINDING) <
						path = <"/data[at0001]/events[at0002]/data[at0003]/items[at0004]">
					>
				>
				predicates = <"max(/data/events/time)",...>
			>
		>
		rules = <
			["gt0002"] = (RULE) <
				when = <"$gt0012<=10", "$gt0009.units=='mg/dl'", "$gt0004==null">
				then = <"$gt0010.value=$currentDateTime.value", "$gt0004.precision=2", "$gt0004.magnitude=(((((($gt0006.magnitude^0.5)*0.56)+(($gt0007.magnitude^0.5)*0.28))+(0.36*($gt0009.magnitude+1)))+(0.14*$gt0012.magnitude))+0.96)">
				priority = <1>
			>
		>
	>
	ontology = (GUIDE_ONTOLOGY) <
		term_definitions = <
			["en"] = (TERM_DEFINITION) <
				terms = <
					["gt0001"] = (TERM) <
						text = <"DAS28-CRP Calculator for Rheumatoid Arthritis">
						description = <"Disease activity score 28-CRP (DAS28-CRP) is a calculated score for assessing disease activity in individuals with rheumatoid arthritis (RA). It is calculated from a formula using four parameters: the number of tender (0 - 28) and swollen (0 - 28) joints the patient has, the visual analogue scale score (0 - 10) for patient global assessment of the level of disease activity [0.0 = low disease activity/patient doing very well; 10.0 = high disease activity/patient doing very poor], and the patient's circulating level of the inflammatory biomarker C-reactive protein (CRP).">
					>
					["gt0002"] = (TERM) <
						text = <"Calculate DAS28-CRP">
						description = <"Contains the application logic for calculating DAS28-CRP.">
					>
					["gt0004"] = (TERM) <
						text = <"DAS28-CRP">
						description = <"Disease activity score 28 (CRP) is calculated from a formula that includes tender joint count (TJC), swollen joint count (SJC), patient assessment of global disease activity (PtGDA) and C-reactive protein (CRP).">
					>
					["gt0006"] = (TERM) <
						text = <"Tender joint count (TJC)">
						description = <"Total number of tender joints of the possible 28 (on the left side and right side).">
					>
					["gt0007"] = (TERM) <
						text = <"Swollen joint count (SJC)">
						description = <"Total number of swollen joints of the possible 28 (on the left side and right side).">
					>
					["gt0009"] = (TERM) <
						text = <"C-reactive protein">
						description = <"Test result - C-reactive protein">
					>
					["gt0010"] = (TERM) <
						text = <"Event time">
						description = <"The exact time of a single timed event during the Observation. Can represent either a specific point-in-time or an interval event.">
					>
					["gt0012"] = (TERM) <
						text = <"PtGDA score">
						description = <"Considering all the ways arthritis affects you, how well are you doing? (0.0 = very well; 10.0 = very poor)">
					>
				>
			>
		>
	>
>
