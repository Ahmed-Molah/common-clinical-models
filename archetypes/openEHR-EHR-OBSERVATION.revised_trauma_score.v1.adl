archetype (adl_version=1.4)
	openEHR-EHR-OBSERVATION.revised_trauma_score.v1

concept
	[at0000]	-- Revised trauma score (RTS)
language
	original_language = <[ISO_639-1::en]>
description
	original_author = <
		["name"] = <"Eneimi Allwell-Brown">
		["email"] = <"info@cambio.se">
		["organisation"] = <"Cambio Healthcare Systems">
		["date"] = <"2017-01-22">
	>
	details = <
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"To record revised trauma score (RTS) and triage-revised trauma score (T-RTS), for pre-hospital triage, quantifying injury severity, and assessing probability of survival/death following trauma.">
			use = <"To record data for RTS and T-RTS, based on Glasgow Coma Scale (GCS) score, respiratory rate (RR) and systolic blood pressure (SBP) values taken at first patient contact following injury. GCS, RR and SBP are coded as follows;

Glasgow Coma Scale(GCS)     Systolic Blood Pressure(SBP)     Respiratory Rate(RR)     Coded Value(c)
13-15                                          >89                                                10-29                               4
9-12                                            76-89                                             >29                                  3
6-8                                              50-75                                             6-9                                   2
4-5                                              1-49                                               1-5                                   1
3                                                  0                                                     0                                      0

RTS = (0.9368 * cGCS) + (0.7326 * cSBP) + (0.2908 * cRR)  --> scored 0 - 7.8408 (includes non-integer values), and heavily weighted towards the Glasgow Coma Scale to compensate for major head injury without multisystem injury or major physiological changes. RTS is used to quantify the severity of injury and to predict survival/death outcome following trauma; higher RTS scores are associated with greater probability of survival (RTS of 4 => 60% probability of survival). RTS determined probability of survival is stored in openEHR-EHR-EVALUATION.revised_trauma_score.v1 archetype.

T-RTS = cGCS + cSBP + cRR  --> scored 0 - 12 (integer values only), and used for triage in the pre-hospital setting. Patients with T-RTS = 1 - 10 (T1) require immediate life-saving intervention; T-RTS = 11 (T2) require urgent intervention within two to four hours, T-RTS = 12 (T3) can have delayed care, while T-RTS = 0 implies a dead patient.

RTS may be combined with Injury Severity Score (ISS), an anatomic trauma scoring system, for epidemiologic studies as well as studies evaluating different strategies or systems of trauma care. RTS may also be used along with ISS and patient age to calculate Trauma and Injury Severity Score (TRISS) which is used to determine probability of patient survival.">
			keywords = <"TRISS", "ISS", "trauma", "injury", "triage", "trauma score", "trauma outcome">
			misuse = <"Scoring may be innacurate for patients who are intubated and mechanically ventilated.">
			copyright = <"Â© Cambio Healthcare Systems">
		>
	>
	lifecycle_state = <"AuthorDraft">
	other_contributors = <>
	other_details = <
		["references"] = <"Champion HR, Sacco WJ, Copes WS, Gann DS, Gennarelli TA, Flanagan ME. A revision of the Trauma Score. Journal of Trauma and Acute Care Surgery. 1989 May 1;29(5):623-9.

Moore L, Lavoie A, LeSage N, Abdous B, Bergeron E, Liberman M, Emond M. Statistical validation of the revised trauma score. Journal of Trauma and Acute Care Surgery. 2006 Feb 1;60(2):305-11.">
		["MD5-CAM-1.0.1"] = <"80D6D665B836A97CD9D8885C74048125">
	>

definition
	OBSERVATION[at0000] matches {	-- Revised trauma score (RTS)
		data matches {
			HISTORY[at0001] matches {	-- Event Series
				events cardinality matches {1..*; unordered} matches {
					EVENT[at0002] occurrences matches {0..1} matches {	-- Any event
						data matches {
							ITEM_TREE[at0003] matches {	-- Tree
								items cardinality matches {0..*; unordered} matches {
									ELEMENT[at0004] occurrences matches {0..1} matches {	-- cGCS
										value matches {
											0|[local::at0009], 	-- 3
											1|[local::at0010], 	-- 4 - 5
											2|[local::at0011], 	-- 6 - 8
											3|[local::at0012], 	-- 9 - 12
											4|[local::at0013]  	-- 13 - 15
										}
									}
									ELEMENT[at0005] occurrences matches {0..1} matches {	-- cSBP
										value matches {
											0|[local::at0014], 	-- 0 (mmHg)
											1|[local::at0015], 	-- 1 - 49 (mmHg)
											2|[local::at0016], 	-- 50 - 75 (mmHg)
											3|[local::at0017], 	-- 76 - 89 (mmHg)
											4|[local::at0018]  	-- > 89 (mmHg)
										}
									}
									ELEMENT[at0006] occurrences matches {0..1} matches {	-- cRR
										value matches {
											0|[local::at0019], 	-- 0 (breaths/min)
											1|[local::at0020], 	-- 1 - 5 (breaths/min)
											2|[local::at0021], 	-- 6 - 9 (breaths/min)
											3|[local::at0022], 	-- > 29 (breaths/min)
											4|[local::at0023]  	-- 10 - 29 (breaths/min)
										}
									}
									ELEMENT[at0007] occurrences matches {0..1} matches {	-- Revised trauma score (RTS)
										value matches {
											C_DV_QUANTITY <
											>
										}
									}
									ELEMENT[at0024] occurrences matches {0..1} matches {	-- Triage-Revised trauma score (T-RTS)
										value matches {
											DV_COUNT matches {
												magnitude matches {|0..12|}
											}
										}
									}
									ELEMENT[at0008] occurrences matches {0..1} matches {	-- Comment
										value matches {
											DV_TEXT matches {*}
										}
									}
								}
							}
						}
					}
				}
			}
		}
	}

ontology
	term_definitions = <
		["en"] = <
			items = <
				["at0000"] = <
					text = <"Revised trauma score (RTS)">
					description = <"Revised Trauma Score (RTS) is a physiologic score of injury severity performed at first patient contact following trauma, and it numerically summarizes assessments of circulatory (Systolic Blood Pressure), respiratory (Respiratory Rate) and central nervous system (Glasgow Coma Scale score) function, using coded and weighted forms of the three physiologic variables. It has high inter-rater ability and is used to quantify the severity of injury, to predict survival/death outcome and (with unweighted forms of the variables) as a triage tool in the pre-hospital setting (Triage-RTS). RTS is scored from 0 - 7.8408 (includes non-integer values), and higher RTS scores are associated with greater probability of survival. T-RTS is scored from 0 - 12 (only integer values), and patients with T-RTS = 1 - 10 (T1) require immediate life-saving intervention; T-RTS = 11 (T2) require urgent intervention within two to four hours, T-RTS = 12 (T3) can have delayed care, while T-RTS = 0 implies a dead patient.">
				>
				["at0001"] = <
					text = <"Event Series">
					description = <"@ internal @">
				>
				["at0002"] = <
					text = <"Any event">
					description = <"*">
				>
				["at0003"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0004"] = <
					text = <"cGCS">
					description = <"Points assigned based on Glasgow Coma Scale score.">
				>
				["at0005"] = <
					text = <"cSBP">
					description = <"Points assigned based on systolic blood pressure (mmHg).">
				>
				["at0006"] = <
					text = <"cRR">
					description = <"Points assigned based on respiratory rate (breaths/min).">
				>
				["at0007"] = <
					text = <"Revised trauma score (RTS)">
					description = <"Calculated revised trauma score.">
				>
				["at0008"] = <
					text = <"Comment">
					description = <"Additional information about any of the recordings.">
				>
				["at0009"] = <
					text = <"3">
					description = <"Points assigned for GCS score = 0. ">
				>
				["at0010"] = <
					text = <"4 - 5">
					description = <"Points assigned for GCS score = 4 - 5. ">
				>
				["at0011"] = <
					text = <"6 - 8">
					description = <"Points assigned for GCS score = 6 - 8. ">
				>
				["at0012"] = <
					text = <"9 - 12">
					description = <"Points assigned for GCS score = 9 - 12. ">
				>
				["at0013"] = <
					text = <"13 - 15">
					description = <"Points assigned for GCS score = 15 - 18. ">
				>
				["at0014"] = <
					text = <"0 (mmHg)">
					description = <"Points assigned for systolic blood pressure = 0 mmHg. breaths/min">
				>
				["at0015"] = <
					text = <"1 - 49 (mmHg)">
					description = <"Points assigned for systolic blood pressure = 1 - 49 mmHg. ">
				>
				["at0016"] = <
					text = <"50 - 75 (mmHg)">
					description = <"Points assigned for systolic blood pressure = 50 - 75 mmHg. ">
				>
				["at0017"] = <
					text = <"76 - 89 (mmHg)">
					description = <"Points assigned for systolic blood pressure = 76 - 89 mmHg. ">
				>
				["at0018"] = <
					text = <"> 89 (mmHg)">
					description = <"Points assigned for systolic blood pressure > 89 mmHg. ">
				>
				["at0019"] = <
					text = <"0 (breaths/min)">
					description = <"Points assigned for respiratory rate = 0 breaths/min">
				>
				["at0020"] = <
					text = <"1 - 5 (breaths/min)">
					description = <"Points assigned for respiratory rate = 1 - 5  breaths/min">
				>
				["at0021"] = <
					text = <"6 - 9 (breaths/min)">
					description = <"Points assigned for respiratory rate = 6 - 9 breaths/min">
				>
				["at0022"] = <
					text = <"> 29 (breaths/min)">
					description = <"Points assigned for respiratory rate > 29 breaths/min">
				>
				["at0023"] = <
					text = <"10 - 29 (breaths/min)">
					description = <"Points assigned for respiratory rate = 10 - 29 breaths/min">
				>
				["at0024"] = <
					text = <"Triage-Revised trauma score (T-RTS)">
					description = <"Calculated triage-revised trauma score.">
				>
			>
		>
	>
